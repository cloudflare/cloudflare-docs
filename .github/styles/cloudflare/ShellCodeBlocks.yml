---
# Warning: cloudflare.ShellCodeBlocks
#
# Checks that all `sh` codeblocks contain at least one `$`, to indicate a command.
#
# For a list of all options, see https://vale.sh/docs/topics/styles/
extends: script
message: "**Warning**: `sh` codeblocks must include a `$` character at the beginning of a command, otherwise it can't be copied. If this is a multi-line codeblock, please use the `bash` language. For more information, refer to [Code block guidelines](https://developers.cloudflare.com/style-guide/formatting/code-block-guidelines/)"
level: warning
scope: raw
script: |
  text := import("text")
  matches := []

  open := false
  closed := false
  dollar := false
  blockText := []

  reset := func() {
    open = false
    closed = false
    dollar = false
    blockText = []
  }

  for line in text.split(scope, "\n") {
    trimmed := text.trim_space(line)

    if !open && trimmed == "```sh" { open = true }

    if open {
      blockText = append(blockText, line)
      if !dollar && text.contains(trimmed, "$ ") { dollar = true }
      if !closed && trimmed == "```" { closed = true }
    }

    if open && closed {
      if !dollar {
        block := text.join(blockText, "\n")
        start := text.index(scope, block)
        matches = append(matches, { begin: start, end: start + len(block) })
      }

      reset()
    }
  }